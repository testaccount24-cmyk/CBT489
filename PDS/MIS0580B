
       TITLE '  MIS0580B: list move requests'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0580B.
      ******************************************************************
      *@@@ II list move requests from SUT delivery list
      ******************************************************************
      *
      *  I AM INTERESTED IN KNOWING IF/WHERE THESE PROGRAMS
      *  ARE IN USE.     EMAIL:  jiannon@me.com
      *
      ******************************************************************
      * DATE WRITTEN: 04/15/98
      * AUTHOR      : JIM IANNONE
      *               LOOP THROUGH A SUT CHANGE REPORT AND PARSE
      *               OUT MOVE REQUESTS AS REQUESTED BY ENTRIES
      *               IN A STATIC PARM.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS PAGE-TOP.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT SUTIN                ASSIGN TO SUTIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SUTO03               ASSIGN TO SUTO03
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PARM01               ASSIGN TO PARM01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SMSNAME              ASSIGN TO SMSNAME
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  SUTIN
           RECORDING MODE F.
       01  SUTIN-RECORD.
           05  SUTIN-RCD1              PIC  X(133).
           05  FILLER REDEFINES SUTIN-RCD1.
               10  FILLER              PIC  X(05).
               10  SUTIN-LIB           PIC  X(08).
               10  FILLER              PIC  X(05).
               10  SUTIN-MARK1         PIC  X.
               10  FILLER              PIC  XXX.
               10  SUTIN-ELEMENT       PIC  X(08).
               10  FILLER              PIC  XXX.
               10  SUTIN-MARK2         PIC  X.
               10  FILLER              PIC  XX.
               10  SUTIN-LIB-NAME      PIC  X(44).
               10  FILLER              PIC  XXX.
               10  SUTIN-MARK3         PIC  X.
               10  FILLER              PIC  XXX.
               10  SUTIN-TARGET        PIC  X(05).
               10  FILLER              PIC  XX.
               10  SUTIN-MARK4         PIC  X.
               10  FILLER              PIC  XX.
               10  SUTIN-AREA1         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ1     PIC  X(05).
               10  FILLER              PIC  XXX.
               10  SUTIN-AREA2         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ2     PIC  X(05).
               10  FILLER              PIC  XXX.
               10  SUTIN-AREA3         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ3     PIC  X(05).

       FD  PARM01
           RECORDING MODE F.
       01  PARM01-RECORD.
           05  PARM01-DOCU             PIC  X.
           05  FILLER                  PIC  X.
           05  PARM01-MOVE-REQ         PIC  X(08).
           05  FILLER                  PIC  X(62).
           05  FILLER                  PIC  X(08).

       FD  SUTO03
           RECORDING MODE F.
       01  SUTO03-RECORD.
           05  SUTO03-SUT-NAME         PIC  X(14).
           05  FILLER                  PIC  X.
           05  SUTO03-MOVE-REQ         PIC  X(08).
           05  FILLER                  PIC  X.
           05  SUTO03-APPLY            PIC  X(106).
           05  FILLER                  PIC  XX.

       FD  SMSNAME
           RECORDING MODE F.
       01  SMSNAME-RECORD.
           05  SMSNAME-PRE             PIC  X(12).
           05  SMSNAME-NAME            PIC  X(30).
           05  FILLER                  PIC  X(38).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING STORAGE MIS0580B START '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-PARM01-MAX           PIC  9(05)    VALUE ZERO.
           05  WS-SUT-NAME             PIC  X(20)    VALUE SPACE.
           05  WS-SMSNAME              PIC  X(30)    VALUE SPACE.
           05  WS-MOVE-REQ             PIC  X(08)    VALUE SPACE.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD3-CNT       PIC S9(07)    VALUE ZERO.
           05  WS-SKIP-CNT             PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-SUTIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-SUTIN                      VALUE 'Y'.
               88  MORE-SUTIN-RCDS                   VALUE 'N'.
           05  WS-TOP-SW               PIC  X        VALUE SPACE.
           05  WS-DASH-SW              PIC  X        VALUE SPACE.
           05  WS-DIFF-SW              PIC  X        VALUE SPACE.
           05  WS-REPORT-FOUND-SW      PIC  X        VALUE SPACE.
           05  WS-MV-REQ-SW            PIC  X        VALUE SPACE.
           05  WS-END-MV-REQ           PIC  X        VALUE SPACE.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(04)   VALUE ZERO.

       01  WS-TABLES.
           05  WS-MOVE-REQUEST-TABLE   PIC  X(800)   VALUE SPACE.
           05  WS-MV-REQ REDEFINES WS-MOVE-REQUEST-TABLE
                                       PIC  X(08)
                                       OCCURS 100 TIMES.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0580B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-SUTIN.

           DISPLAY '**** RECORDS IN SUTIN       ' WS-READ-RCD-CNT.
           DISPLAY '**** PARM MOVE REQUESTS     ' WS-PARM01-MAX.
           DISPLAY '**** PRINTED MOVE REQUESTS  ' WS-WRITE-RCD3-CNT.
           DISPLAY '**** MIS0580B EOJ ENCOUNTERED '.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0580B COND CODE ' WS-RETURN-CODE
                       ' SENT BY PROGRAM'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  SUTIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0580B OPEN ERROR ON SUTIN'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT  PARM01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0580B OPEN ERROR ON PARM01'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT SMSNAME.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0580B OPEN ERROR ON SMSNAME'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT SUTO03.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0580B OPEN ERROR ON SUTO03'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE SUTO03.
           CLOSE SMSNAME.
           CLOSE PARM01.
           CLOSE SUTIN.

       0400-PROCESS-SUTIN.
           IF WS-RETURN-CODE = ZERO
               MOVE 'UNKNOWN SUT DELIVERY'    TO WS-SMSNAME
               PERFORM 0500-FIND-NAME THRU
                       0500-FIND-NAME-EXIT.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0550-LOAD-PARM01 THRU
                       0550-LOAD-PARM01-EXIT.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
                       UNTIL END-OF-SUTIN.
           IF WS-DIFF-SW = 'Y'
               MOVE '  NON-STANDARD DELIVERY REPORT ' TO SUTO03-RECORD
               PERFORM 1300-WRITE-SUTO03.
           IF WS-REPORT-FOUND-SW = SPACE
               MOVE '  NO RECOGNIZED REPORT FORMAT FOUND'
                                    TO SUTO03-RECORD
               PERFORM 1300-WRITE-SUTO03.

       0500-FIND-NAME.
           READ SMSNAME.
           IF WS-STAT = 10
               GO TO 0500-FIND-NAME-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0580B READ ERROR ON SMSNAME'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE
               GO TO 0500-FIND-NAME-EXIT.

           IF SMSNAME-PRE(1: 5) = '00000'
               MOVE SMSNAME-NAME          TO WS-SMSNAME
               GO TO 0500-FIND-NAME-EXIT.
           GO TO 0500-FIND-NAME.

       0500-FIND-NAME-EXIT. EXIT.

      ****************************************
      *    LOAD PARM01
      ****************************************
       0550-LOAD-PARM01.
           READ PARM01.
           IF WS-STAT = 10
               MOVE WS-SUB1                   TO WS-PARM01-MAX
               GO TO 0550-LOAD-PARM01-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0580B READ ERROR ON PARM01'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0550                     TO WS-RETURN-CODE
               GO TO 0550-LOAD-PARM01-EXIT.

           IF PARM01-DOCU = '*'
               GO TO 0550-LOAD-PARM01.
           IF WS-SUB1 < 100
               ADD 1                          TO WS-SUB1
               MOVE PARM01-MOVE-REQ           TO WS-MV-REQ (WS-SUB1).
           GO TO 0550-LOAD-PARM01.

       0550-LOAD-PARM01-EXIT. EXIT.

       0600-SELECT.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               GO TO 0600-SELECT-EXIT.

           IF WS-SKIP-CNT > ZERO
               SUBTRACT 1 FROM WS-SKIP-CNT
               GO TO 0600-SELECT-EXIT.

      ****************************************
      *    this logic is for the delivery reports which are in
      *    a different format than regular SUT delivery reports.
      *+**************************************
           IF SUTIN-RECORD(56: 19) = 'SMS DELIVERY REPORT'
               MOVE 'Y'                       TO WS-REPORT-FOUND-SW
               MOVE 'Y'                       TO WS-DIFF-SW.
           IF WS-DIFF-SW = 'Y'
               GO TO 0600-SELECT-EXIT.

      ****************************************
      *    'events contained' is the signal for a regular sut
      *    delivery report
      *+**************************************
           IF SUTIN-RECORD(22: 16) = 'Events contained'
                                  OR 'EVENTS CONTAINED'
               MOVE 'Y'                       TO WS-REPORT-FOUND-SW
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD(24: 20)      TO WS-SUT-NAME
               GO TO 0600-SELECT-EXIT.

       0600-CONTINUE.
           IF END-OF-SUTIN
               GO TO 0600-SELECT-EXIT.
           IF SUTIN-RECORD(02: 16) = 'MOVE REQUEST NO.'
               PERFORM 1000-READ-SUTIN
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD(10: 8)       TO WS-MOVE-REQ
               MOVE SPACE                     TO WS-MV-REQ-SW
               PERFORM 0700-TEST-MV-REQ
                   VARYING WS-SUB1 FROM 1 BY 1
                   UNTIL WS-SUB1 > WS-PARM01-MAX
               IF WS-MV-REQ-SW = 'Y'
                   PERFORM 1000-READ-SUTIN
                   MOVE SPACE                 TO WS-END-MV-REQ
                   MOVE 'Y'                   TO WS-TOP-SW
                   PERFORM 0800-PRINT-MV-REQ
                       UNTIL WS-END-MV-REQ = 'Y'
                   GO TO 0600-CONTINUE.

       0600-SELECT-EXIT. EXIT.

       0700-TEST-MV-REQ.
           IF WS-MOVE-REQ = WS-MV-REQ (WS-SUB1)
               MOVE 'Y'                       TO WS-MV-REQ-SW
               MOVE WS-PARM01-MAX             TO WS-SUB1.

       0800-PRINT-MV-REQ.
           MOVE SPACE                         TO SUTO03-RECORD.
           MOVE SUTIN-RCD1(2: 106)            TO SUTO03-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO03-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO03-SUT-NAME.
           IF WS-TOP-SW = 'Y'
               PERFORM 1400-WRITE-SUTO03-TOP
               MOVE SPACE                     TO WS-TOP-SW
           ELSE
               PERFORM 1300-WRITE-SUTO03.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               MOVE 'Y'                       TO WS-END-MV-REQ.
           IF SUTIN-RECORD(02: 16) = 'MOVE REQUEST NO.'
               MOVE 'Y'                       TO WS-END-MV-REQ.
           IF SUTIN-RECORD(63: 10) = '----------'
               IF WS-DASH-SW = 'Y'
                   MOVE 'Y'                   TO WS-END-MV-REQ
               ELSE
                   MOVE 'Y'                   TO WS-DASH-SW
           ELSE
               MOVE SPACE                     TO WS-DASH-SW.

       1000-READ-SUTIN.
           READ SUTIN.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-SUTIN-SW
           ELSE
               DISPLAY '**** MIS0580B READ ERROR ON SUTIN '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE 'Y'                       TO WS-END-SUTIN-SW
               MOVE +1000                     TO WS-RETURN-CODE.

       1300-WRITE-SUTO03.
           WRITE SUTO03-RECORD AFTER ADVANCING 1.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0580B WRITE ERROR ON SUTO03'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1300                     TO WS-RETURN-CODE.

       1400-WRITE-SUTO03-TOP.
           WRITE SUTO03-RECORD AFTER ADVANCING PAGE-TOP.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0580B WRITE ERROR ON SUTO03'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1400                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-WRITE-RCD3-CNT.

      ************************************ END MIS0580B
