
       TITLE '  MIS0490B: CREATE SUT TARGET COPY JCL'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0490B.
      ******************************************************************
      *@@@ II CREATE SUT TARGET COPY JCL
      ******************************************************************
      *
      *  I AM INTERESTED IN KNOWING IF/WHERE THESE PROGRAMS
      *  ARE IN USE.     EMAIL:  jiannon@me.com
      *
      ******************************************************************
      * DATE WRITTEN: 08/27/96
      * AUTHOR      : JIM IANNONE
      *               LOOP THROUGH A LIST OF SUT TARGET FILES
      *               AND BUILD A LIST OF FILES FOR COPYING
      *               AND RENAMING USING DFDSS.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT SMSBFILO             ASSIGN TO SMSBFILO
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SMSBFILC             ASSIGN TO SMSBFILC
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  SMSBFILO
           RECORDING MODE F.
       01  SMSBFILO-RECORD.
         02  SMSBFILO-RCD.
           05  SMSBFILO-SKIP           PIC  X(05).
           05  FILLER                  PIC  X(19).
           05  SMSBFILO-DBHL1          PIC  X(04).
           05  SMSBFILO-DOT1           PIC  X.
           05  FILLER                  PIC  X.
           05  SMSBFILO-DBHL2          PIC  X(04).
           05  SMSBFILO-DOT2           PIC  X.
           05  FILLER                  PIC  X(45).
         02  SMSBFILO-RCD2 REDEFINES SMSBFILO-RCD.
           05  FILLER                  PIC  X(23).
           05  SMSBFILO-HLQ            PIC  X(06).
           05  SMSBFILO-REMAIN         PIC  X(42).
           05  FILLER                  PIC  X(09).

       FD  SMSBFILC
           RECORDING MODE F.
       01  SMSBFILC-RECORD.
           05  FILLER                  PIC  X(20).
           05  SMSBFILC-LINE           PIC  X(50).
           05  FILLER                  PIC  X(10).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING STORAGE MIS0490B START '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-WRITE-RCD-CNT        PIC S9(07)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-SMSBFILO-SW      PIC  X        VALUE 'N'.
               88  END-OF-SMSBFILO                   VALUE 'Y'.
               88  MORE-SMSBFILO-RCDS                VALUE 'N'.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0490B END  '.

       LINKAGE SECTION.

       01  PARM-FIELD.
           05  PARM-LENGTH             PIC S9(04)    COMP.
           05  PARM-SUFFIX             PIC  X(06).

      /
       PROCEDURE DIVISION USING PARM-FIELD.

       0100-PROGRAM-ENTRY.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-SMSBFILO.

           DISPLAY '**** FILES TO COPY      ' WS-WRITE-RCD-CNT.
           DISPLAY '**** MIS0490B EOJ ENCOUNTERED '.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0490B COND CODE ' WS-RETURN-CODE
                       ' SENT BY PROGRAM'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  SMSBFILO.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0490B OPEN ERROR ON SMSBFILO'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT SMSBFILC.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0490B OPEN ERROR ON SMSBFILC'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE SMSBFILC.
           CLOSE SMSBFILO.

       0400-PROCESS-SMSBFILO.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
                       UNTIL END-OF-SMSBFILO.

       0600-SELECT.
           PERFORM 1000-READ-SMSBFILO.
           IF END-OF-SMSBFILO
               GO TO 0600-SELECT-EXIT.

           IF SMSBFILO-SKIP NOT = SPACE
               GO TO 0600-SELECT-EXIT.
           IF (SMSBFILO-DBHL1 = 'DSMS' OR 'LSMS')
               AND (SMSBFILO-DBHL2 = 'DSMS' OR 'LSMS' OR 'LSYS')
               AND (SMSBFILO-DOT1 = '.')
               AND (SMSBFILO-DOT2 = '.')
               GO TO 0600-SELECT-EXIT.

           IF SMSBFILO-DBHL1 = SPACE
               GO TO 0600-SELECT-EXIT.

           MOVE SPACE                         TO SMSBFILC-RECORD.
           STRING '('             DELIMITED BY SIZE
                  SMSBFILO-HLQ    DELIMITED BY SIZE
                  SMSBFILO-REMAIN DELIMITED BY SIZE
                  INTO SMSBFILC-LINE.
           PERFORM 1100-WRITE-SMSBFILC.
           ADD 1                              TO WS-WRITE-RCD-CNT.
           MOVE SPACE                         TO SMSBFILC-RECORD.
           STRING '   '           DELIMITED BY SIZE
                  SMSBFILO-HLQ    DELIMITED BY SPACE
                  PARM-SUFFIX     DELIMITED BY SIZE
                  '.'             DELIMITED BY SIZE
                  SMSBFILO-REMAIN DELIMITED BY SPACE
                  ')'             DELIMITED BY SIZE
                  ' -'            DELIMITED BY SIZE
                  INTO SMSBFILC-LINE.
           PERFORM 1100-WRITE-SMSBFILC.

       0600-SELECT-EXIT. EXIT.

       1000-READ-SMSBFILO.
           READ SMSBFILO.
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-SMSBFILO-SW
           ELSE
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0490B READ ERROR ON SMSBFILO '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE 'Y'                       TO WS-END-SMSBFILO-SW
               MOVE +1000                     TO WS-RETURN-CODE.

       1100-WRITE-SMSBFILC.
           WRITE SMSBFILC-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0490B WRITE ERROR ON SMSBFILC'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1100                     TO WS-RETURN-CODE.

      ************************************ END MIS0490B
