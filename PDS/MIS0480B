
       TITLE '  MIS0480B: pull element list from sut delivery list'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0480B.
      ******************************************************************
      *@@@ II pull element list from sut delivery list
      ******************************************************************
      *
      *  I AM INTERESTED IN KNOWING IF/WHERE THESE PROGRAMS
      *  ARE IN USE.     EMAIL:  jiannon@me.com
      *
      ******************************************************************
      * DATE WRITTEN: 08/09/96
      * AUTHOR      : JIM IANNONE
      *               LOOP THROUGH A SUT CHANGE REPORT AND PARSE
      *               RECORDS INTO A USEABLE FORMAT FOR FUTURE
      *               COMPARES.
      *
      ******************************************************************
      *               SUTS are delivered to 'ICO' environments
      *               by Shared Medical Systems in Malvern, PA.
      *
      *               A report listing what is delivered is presented
      *               in electronic form on the tape.  This
      *               program will loop through that report and
      *               further clarify what has been delivered.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT SUTIN                ASSIGN TO SUTIN
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SUTO01               ASSIGN TO SUTO01
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SUTO02               ASSIGN TO SUTO02
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SUTO03               ASSIGN TO SUTO03
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SMSNAME              ASSIGN TO SMSNAME
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

       DATA DIVISION.
       FILE SECTION.

       FD  SUTIN
           RECORDING MODE F.
       01  SUTIN-RECORD.
           05  SUTIN-RCD1              PIC  X(133).
           05  FILLER REDEFINES SUTIN-RCD1.
               10  FILLER              PIC  X(05).
               10  SUTIN-LIB           PIC  X(08).
               10  FILLER              PIC  X(05).
               10  SUTIN-MARK1         PIC  X.
               10  FILLER              PIC  XXX.
               10  SUTIN-ELEMENT       PIC  X(08).
               10  FILLER              PIC  XXX.
               10  SUTIN-MARK2         PIC  X.
               10  FILLER              PIC  XX.
               10  SUTIN-LIB-NAME      PIC  X(44).
               10  FILLER              PIC  XXX.
               10  SUTIN-MARK3         PIC  X.
               10  FILLER              PIC  XXX.
               10  SUTIN-TARGET        PIC  X(05).
               10  FILLER              PIC  XX.
               10  SUTIN-MARK4         PIC  X.
               10  FILLER              PIC  XX.
               10  SUTIN-AREA1         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ1     PIC  X(05).
               10  FILLER              PIC  XXX.
               10  SUTIN-AREA2         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ2     PIC  X(05).
               10  FILLER              PIC  XXX.
               10  SUTIN-AREA3         PIC  XXX.
               10  FILLER              PIC  XX.
               10  SUTIN-MOVE-REQ3     PIC  X(05).

       FD  SUTO01
           RECORDING MODE F.
       01  SUTO01-RECORD.
           05  FILLER              PIC  X.
           05  SUTO01-SUT-NAME     PIC  X(20).
           05  FILLER              PIC  X.
           05  SUTO01-LIB          PIC  X(08).
           05  FILLER              PIC  X.
           05  SUTO01-MARK1        PIC  X.
           05  FILLER              PIC  X.
           05  SUTO01-ELEMENT      PIC  X(08).
           05  FILLER              PIC  X.
           05  SUTO01-MARK2        PIC  X.
           05  FILLER              PIC  X.
           05  SUTO01-LIB-NAME     PIC  X(44).
           05  SUTO01-TRAILER.
               10  FILLER              PIC  X.
               10  SUTO01-MARK3        PIC  X.
               10  FILLER              PIC  X.
               10  SUTO01-TARGET       PIC  X(05).
               10  FILLER              PIC  X.
               10  SUTO01-MARK4        PIC  X.
               10  FILLER              PIC  X.
               10  SUTO01-AREA1        PIC  XXX.
               10  FILLER              PIC  X.
               10  SUTO01-MOVE-REQ1    PIC  X(05).
               10  FILLER              PIC  X.
               10  SUTO01-AREA2        PIC  XXX.
               10  FILLER              PIC  X.
               10  SUTO01-MOVE-REQ2    PIC  X(05).
               10  FILLER              PIC  X.
               10  SUTO01-AREA3        PIC  XXX.
               10  FILLER              PIC  X.
               10  SUTO01-MOVE-REQ3    PIC  X(05).
               10  FILLER              PIC  X(04).
           05  SUTO01-TRAILER2 REDEFINES SUTO01-TRAILER.
               10  SUTO01-DESCRIPTION  PIC  X(40).
               10  FILLER              PIC  X(04).

       FD  SUTO02
           RECORDING MODE F.
       01  SUTO02-RECORD.
           05  SUTO02-SUT-NAME         PIC  X(20).
           05  FILLER                  PIC  X.
           05  SUTO02-MOVE-REQ         PIC  X(08).
           05  FILLER                  PIC  X.
           05  SUTO02-APPLY            PIC  X(80).
           05  FILLER                  PIC  X(22).

       FD  SUTO03
           RECORDING MODE F.
       01  SUTO03-RECORD.
           05  SUTO03-SUT-NAME         PIC  X(20).
           05  FILLER                  PIC  X.
           05  SUTO03-MOVE-REQ         PIC  X(08).
           05  FILLER                  PIC  X.
           05  SUTO03-APPLY            PIC  X(80).
           05  FILLER                  PIC  X(22).

       FD  SMSNAME
           RECORDING MODE F.
       01  SMSNAME-RECORD.
           05  SMSNAME-PRE             PIC  X(12).
           05  SMSNAME-NAME            PIC  X(30).
           05  FILLER                  PIC  X(38).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING STORAGE MIS0480B START '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-MARK                 PIC  X        VALUE X'4F'.
           05  WS-SUT-NAME             PIC  X(20)    VALUE SPACE.
           05  WS-SMSNAME              PIC  X(30)    VALUE SPACE.
           05  WS-MOVE-REQ             PIC  X(08)    VALUE SPACE.
           05  WS-HOLD-LIB             PIC  X(08)    VALUE SPACE.
           05  WS-HOLD-ELEMENT         PIC  X(08)    VALUE SPACE.
           05  WS-HOLD-LIB-NAME        PIC  X(44)    VALUE SPACE.
           05  WS-HOLD-DESCRIPTION     PIC  X(40)    VALUE SPACE.
           05  WS-HOLD-SUTIN-RCD       PIC  X(133)   VALUE SPACE.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD1-CNT       PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD2-CNT       PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD3-CNT       PIC S9(07)    VALUE ZERO.
           05  WS-SKIP-CNT             PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-SUTIN-SW         PIC  X        VALUE 'N'.
               88  END-OF-SUTIN                      VALUE 'Y'.
               88  MORE-SUTIN-RCDS                   VALUE 'N'.
           05  WS-NEXT-SW              PIC  X        VALUE SPACE.
           05  WS-DASH-SW              PIC  X        VALUE SPACE.
           05  WS-DIFF-SW              PIC  X        VALUE SPACE.
           05  WS-DIFF-SW2             PIC  X        VALUE SPACE.
           05  WS-REPORT-FOUND-SW      PIC  X        VALUE SPACE.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(04)   VALUE ZERO.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0480B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-SUTIN.

           DISPLAY '**** RECORDS IN SUTIN    ' WS-READ-RCD-CNT.
           DISPLAY '**** ELEMENT RCDS OUT    ' WS-WRITE-RCD1-CNT.
           DISPLAY '**** APPLY RCDS OUT      ' WS-WRITE-RCD2-CNT.
           DISPLAY '**** DELIV DESC RCDS OUT ' WS-WRITE-RCD3-CNT.
           DISPLAY '**** MIS0480B EOJ ENCOUNTERED '.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0480B COND CODE ' WS-RETURN-CODE
                       ' SENT BY PROGRAM'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  SUTIN.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0480B OPEN ERROR ON SUTIN'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT SUTO01.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0480B OPEN ERROR ON SUTO01'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT SUTO02.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0480B OPEN ERROR ON SUTO02'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT SUTO03.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0480B OPEN ERROR ON SUTO03'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT SMSNAME.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0480B OPEN ERROR ON SMSNAME'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.

       0300-CLOSE-FILES.
           CLOSE SMSNAME.
           CLOSE SUTO03.
           CLOSE SUTO02.
           CLOSE SUTO01.
           CLOSE SUTIN.

       0400-PROCESS-SUTIN.
           IF WS-RETURN-CODE = ZERO
               MOVE 'UNKNOWN SUT DELIVERY'    TO WS-SMSNAME
               PERFORM 0500-FIND-NAME THRU
                       0500-FIND-NAME-EXIT.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
                       UNTIL END-OF-SUTIN.
           IF WS-DIFF-SW = 'Y' OR 'E'
               MOVE '  NON-STANDARD DELIVERY REPORT ' TO SUTO02-RECORD
                                                         SUTO03-RECORD
               PERFORM 1200-WRITE-SUTO02
               PERFORM 1300-WRITE-SUTO03.
           IF WS-REPORT-FOUND-SW = SPACE
               MOVE '  NO RECOGNIZED REPORT FORMAT FOUND'
                                    TO SUTO01-RECORD  SUTO02-RECORD
                                       SUTO03-RECORD
               PERFORM 1100-WRITE-SUTO01
               PERFORM 1200-WRITE-SUTO02
               PERFORM 1300-WRITE-SUTO03.

       0500-FIND-NAME.
           READ SMSNAME.
           IF WS-STAT = 10
               GO TO 0500-FIND-NAME-EXIT
           ELSE
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0480B READ ERROR ON SMSNAME'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE
               GO TO 0500-FIND-NAME-EXIT.

           IF SMSNAME-PRE(1: 5) = '00000'
               MOVE SMSNAME-NAME          TO WS-SMSNAME
               GO TO 0500-FIND-NAME-EXIT.
           GO TO 0500-FIND-NAME.

       0500-FIND-NAME-EXIT. EXIT.

       0600-SELECT.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               GO TO 0600-SELECT-EXIT.

           IF WS-SKIP-CNT > ZERO
               SUBTRACT 1 FROM WS-SKIP-CNT
               GO TO 0600-SELECT-EXIT.

      ****************************************
      *    this logic is for the delivery reports which are in
      *    a different format than regular SUT delivery reports.
      *+**************************************
           IF WS-DIFF-SW = 'E'
               GO TO 0600-SELECT-EXIT.
           IF SUTIN-RECORD(56: 19) = 'SMS DELIVERY REPORT'
               MOVE 'Y'                       TO WS-REPORT-FOUND-SW
               MOVE 'Y'                       TO WS-DIFF-SW.
           IF WS-DIFF-SW = 'Y'
               PERFORM 0650-DIFF-REPORT THRU
                       0650-DIFF-REPORT-EXIT
               GO TO 0600-SELECT-EXIT.

      ****************************************
      *    'events contained' is the signal for a regular sut
      *    delivery report
      *+**************************************
           IF SUTIN-RECORD(22: 16) = 'Events contained'
                                  OR 'EVENTS CONTAINED'
               MOVE 'Y'                       TO WS-REPORT-FOUND-SW
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD(24: 20)      TO WS-SUT-NAME
               GO TO 0600-SELECT-EXIT.

           IF SUTIN-RECORD(2: 20) = 'MOVE REQUEST NO.    '
               PERFORM 1000-READ-SUTIN
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD(13: 8)       TO WS-MOVE-REQ
               GO TO 0600-SELECT-EXIT.

           IF SUTIN-RECORD(22: 25) = 'SOFTWARE APPLY PROCEDURES'
               PERFORM 1000-READ-SUTIN
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD              TO WS-HOLD-SUTIN-RCD
               PERFORM 1000-READ-SUTIN
               IF SUTIN-RECORD(63: 10) = '----------'
                   IF WS-HOLD-SUTIN-RCD(2: 15) = 'FOLLOW STANDARD'
                       GO TO 0600-SELECT-EXIT
                   ELSE
                       PERFORM 0700-PRINT-APPLY
                       GO TO 0600-SELECT-EXIT
               ELSE
                   PERFORM 0700-PRINT-APPLY
                   MOVE SPACE                 TO WS-DASH-SW
                   PERFORM 0700-APPLY-LOOP THRU
                           0700-APPLY-EXIT
                           UNTIL WS-DASH-SW = 'Y'.

           IF SUTIN-RECORD(12: 22) = 'DESCRIPTION OF PROBLEM'
               PERFORM 1000-READ-SUTIN
               PERFORM 1000-READ-SUTIN
               MOVE SUTIN-RECORD              TO WS-HOLD-SUTIN-RCD
               PERFORM 1000-READ-SUTIN
               IF SUTIN-RECORD(63: 10) = '----------'
                   PERFORM 0750-PRINT-DESC
                   GO TO 0600-SELECT-EXIT
               ELSE
                   PERFORM 0750-PRINT-DESC
                   MOVE SPACE                 TO WS-DASH-SW
                   PERFORM 0750-DESCRIPTION-LOOP THRU
                           0750-DESCRIPTION-EXIT
                           UNTIL WS-DASH-SW = 'Y'.

           IF SUTIN-MARK1 = WS-MARK
               AND SUTIN-MARK2 = WS-MARK
               AND SUTIN-MARK3 = WS-MARK
               AND SUTIN-MARK4 = WS-MARK
               MOVE SPACE                     TO WS-DASH-SW
               PERFORM 0800-ELEMENT-LOOP THRU
                       0800-ELEMENT-EXIT
                       UNTIL WS-DASH-SW = 'Y'.

           GO TO 0600-SELECT-EXIT.

       0600-CLEAR-SW.
           MOVE SPACE                         TO WS-NEXT-SW.

       0600-SELECT-EXIT. EXIT.

      ****************************************
      *    this logic is for the delivery reports which are in
      *    a different format than regular SUT delivery reports.
      *+**************************************
       0650-DIFF-REPORT.
           IF SUTIN-RECORD(4: 7) = 'LIBRARY'
               PERFORM 1000-READ-SUTIN
               PERFORM 1000-READ-SUTIN
               PERFORM 0650-GET-LIB-NAME
               MOVE 'Y'                       TO WS-DIFF-SW2.
           IF SUTIN-RECORD(59: 16) = 'DELIVERY SUMMARY'
               MOVE 'E'                       TO WS-DIFF-SW
               MOVE SPACE                     TO WS-DIFF-SW2.
           IF SUTIN-RECORD(6: 5) = 'DATE:'
               MOVE SPACE                     TO WS-DIFF-SW2.
           IF SUTIN-RECORD(52: 8) = SPACE
               MOVE SPACE                     TO WS-DIFF-SW2.
           IF WS-DIFF-SW2 = SPACE
               GO TO 0650-DIFF-REPORT-EXIT.

           MOVE SUTIN-RECORD(52: 8)           TO WS-HOLD-ELEMENT.
           MOVE SUTIN-RECORD(64: 40)          TO WS-HOLD-DESCRIPTION.
           PERFORM 0650-DIFF-REPORT-OUT.

       0650-DIFF-REPORT-EXIT. EXIT.

       0650-GET-LIB-NAME.
           MOVE SUTIN-RECORD(4: 44)           TO WS-HOLD-LIB-NAME.
           PERFORM
               VARYING WS-SUB1 FROM 44 BY -1
               UNTIL WS-SUB1 < 1
               IF WS-HOLD-LIB-NAME(WS-SUB1: 1) = '.'
                   ADD 1                      TO WS-SUB1
                   MOVE WS-HOLD-LIB-NAME(WS-SUB1: 8) TO WS-HOLD-LIB
                   MOVE ZERO                  TO WS-SUB1
               END-IF
           END-PERFORM.
           IF WS-HOLD-LIB = 'LOADLIB '
               MOVE 'LOAD    '                TO WS-HOLD-LIB.

       0650-DIFF-REPORT-OUT.
           MOVE SPACE                         TO SUTO01-RECORD.
           MOVE WS-SMSNAME                    TO SUTO01-SUT-NAME.
           MOVE WS-HOLD-LIB                   TO SUTO01-LIB
           MOVE WS-HOLD-ELEMENT               TO SUTO01-ELEMENT.
           MOVE WS-HOLD-LIB-NAME              TO SUTO01-LIB-NAME.
           MOVE WS-HOLD-DESCRIPTION           TO SUTO01-DESCRIPTION.
           PERFORM 1100-WRITE-SUTO01.

       0700-PRINT-APPLY.
           MOVE SPACE                         TO SUTO02-RECORD.
           MOVE WS-HOLD-SUTIN-RCD(2: 80)      TO SUTO02-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO02-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO02-SUT-NAME.
           PERFORM 1200-WRITE-SUTO02.
           MOVE SPACE                         TO SUTO02-RECORD.
           MOVE SUTIN-RCD1(2: 80)             TO SUTO02-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO02-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO02-SUT-NAME.
           PERFORM 1200-WRITE-SUTO02.

       0700-APPLY-LOOP.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               MOVE 'Y'                       TO WS-DASH-SW
               GO TO 0700-APPLY-EXIT.
           IF SUTIN-RECORD(63: 10) = '----------'
               MOVE 'Y'                       TO WS-DASH-SW.
           MOVE SPACE                         TO SUTO02-RECORD.
           MOVE SUTIN-RECORD(2: 80)           TO SUTO02-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO02-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO02-SUT-NAME.
           PERFORM 1200-WRITE-SUTO02.

       0700-APPLY-EXIT. EXIT.

       0750-PRINT-DESC.
           MOVE SPACE                         TO SUTO03-RECORD.
           MOVE WS-HOLD-SUTIN-RCD(2: 80)      TO SUTO03-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO03-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO03-SUT-NAME.
           PERFORM 1300-WRITE-SUTO03.
           MOVE SPACE                         TO SUTO03-RECORD.
           MOVE SUTIN-RCD1(2: 80)             TO SUTO03-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO03-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO03-SUT-NAME.
           PERFORM 1300-WRITE-SUTO03.

       0750-DESCRIPTION-LOOP.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               MOVE 'Y'                       TO WS-DASH-SW
               GO TO 0750-DESCRIPTION-EXIT.
           IF SUTIN-RECORD(63: 10) = '----------'
               MOVE 'Y'                       TO WS-DASH-SW.
           MOVE SPACE                         TO SUTO03-RECORD.
           MOVE SUTIN-RECORD(2: 80)           TO SUTO03-APPLY.
           MOVE WS-MOVE-REQ                   TO SUTO03-MOVE-REQ.
           MOVE WS-SUT-NAME                   TO SUTO03-SUT-NAME.
           PERFORM 1300-WRITE-SUTO03.

       0750-DESCRIPTION-EXIT. EXIT.

       0800-ELEMENT-LOOP.
           PERFORM 1000-READ-SUTIN.
           IF END-OF-SUTIN
               MOVE 'Y'                       TO WS-DASH-SW
               GO TO 0800-ELEMENT-EXIT.
           IF SUTIN-LIB-NAME = SPACE
               GO TO 0800-ELEMENT-EXIT.
           IF SUTIN-LIB = '--------'
               GO TO 0800-ELEMENT-EXIT.
           IF SUTIN-ELEMENT = SPACE
               MOVE 'Y'                       TO WS-DASH-SW
               GO TO 0800-ELEMENT-EXIT.
           IF SUTIN-LIB NOT = SPACE
               MOVE SUTIN-LIB                 TO WS-HOLD-LIB.

       0800-WRITE-RECORD.
           MOVE SPACE                         TO SUTO01-RECORD.
           IF SUTIN-LIB = SPACE
               MOVE WS-HOLD-LIB               TO SUTO01-LIB
           ELSE
               MOVE SUTIN-LIB                 TO SUTO01-LIB.
           MOVE WS-SUT-NAME                   TO SUTO01-SUT-NAME.
           MOVE SUTIN-ELEMENT                 TO SUTO01-ELEMENT.
           MOVE SUTIN-LIB-NAME                TO SUTO01-LIB-NAME.
           MOVE SUTIN-TARGET                  TO SUTO01-TARGET.
           MOVE SUTIN-AREA1                   TO SUTO01-AREA1.
           MOVE SUTIN-MOVE-REQ1               TO SUTO01-MOVE-REQ1.
           MOVE SUTIN-AREA2                   TO SUTO01-AREA2.
           MOVE SUTIN-MOVE-REQ2               TO SUTO01-MOVE-REQ2.
           MOVE SUTIN-AREA3                   TO SUTO01-AREA3.
           MOVE SUTIN-MOVE-REQ3               TO SUTO01-MOVE-REQ3.
           PERFORM 1100-WRITE-SUTO01.

       0800-ELEMENT-EXIT. EXIT.

       1000-READ-SUTIN.
           READ SUTIN.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-SUTIN-SW
           ELSE
               DISPLAY '**** MIS0480B READ ERROR ON SUTIN '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE 'Y'                       TO WS-END-SUTIN-SW
               MOVE +1000                     TO WS-RETURN-CODE.

       1100-WRITE-SUTO01.
           WRITE SUTO01-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0480B WRITE ERROR ON SUTO01'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1100                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-WRITE-RCD1-CNT.

       1200-WRITE-SUTO02.
           WRITE SUTO02-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0480B WRITE ERROR ON SUTO02'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1200                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-WRITE-RCD2-CNT.

       1300-WRITE-SUTO03.
           WRITE SUTO03-RECORD.
           IF WS-STAT > ZERO
               DISPLAY '**** MIS0480B WRITE ERROR ON SUTO03'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +1300                     TO WS-RETURN-CODE.
           ADD 1                              TO WS-WRITE-RCD3-CNT.

      ************************************ END MIS0480B
