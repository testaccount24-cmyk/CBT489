
       TITLE '  JCI0057B: LIST SMS JOBS BY DURATION'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    JCI0057B.
      ******************************************************************
      *@@@ II LIST SMS JOBS BY DURATION
      ******************************************************************
      *
      *  I AM INTERESTED IN KNOWING IF/WHERE THESE PROGRAMS
      *  ARE IN USE.     EMAIL:  jiannon@me.com
      *
      ******************************************************************
      * DATE WRITTEN: 07/10/2000
      * AUTHOR      : JIM IANNONE
      *               SORT THE PDSMS.SUGS.CP01.SCHDREPT(0) FILE
      *               BY START TIME AND DATE.  LIST RUN DURATION.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS PAGE-TOP.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT SCHDRPT              ASSIGN TO SCHDRPT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PRINT-SCHDRPT        ASSIGN TO PRINTER.

           SELECT PRINT-SCHDRP2        ASSIGN TO PRINT2.

           SELECT SORT-SCHDRPT         ASSIGN TO S-SORTWK01.

       DATA DIVISION.
       FILE SECTION.

       FD  SCHDRPT
           RECORDING MODE F.
       01  SCHDRPT-RECORD.
           05  FILLER                  PIC  X(05).
           05  SCHDRPT-STREAM          PIC  X(08).
           05  FILLER                  PIC  X.
           05  SCHDRPT-JOB             PIC  X(08).
           05  FILLER                  PIC  X.
           05  SCHDRPT-STATUS          PIC  X.
           05  FILLER                  PIC  X(19).
           05  SCHDRPT-ST-MDY8         PIC  X(08).
           05  FILLER REDEFINES SCHDRPT-ST-MDY8.
               10  FILLER              PIC  XX.
               10  SCHDRPT-SLASH1      PIC  X.
               10  FILLER              PIC  XX.
               10  SCHDRPT-SLASH2      PIC  X.
               10  FILLER              PIC  XX.
           05  FILLER                  PIC  X.
           05  SCHDRPT-ST-HMS8         PIC  X(08).
           05  FILLER REDEFINES SCHDRPT-ST-HMS8.
               10  FILLER              PIC  XX.
               10  SCHDRPT-COLON1      PIC  X.
               10  FILLER              PIC  XX.
               10  SCHDRPT-COLON2      PIC  X.
               10  FILLER              PIC  XX.
           05  FILLER                  PIC  X(04).
           05  SCHDRPT-END-MDY8        PIC  X(08).
           05  FILLER                  PIC  X.
           05  SCHDRPT-END-HMS8        PIC  X(08).
           05  FILLER                  PIC  X(04).
           05  SCHDRPT-DESC            PIC  X(30).
           05  FILLER                  PIC  X(17).

       FD  PRINT-SCHDRPT
           RECORDING MODE F.
       01  PRINT-AREA.
           05  FILLER                  PIC  X(132).

       FD  PRINT-SCHDRP2
           RECORDING MODE F.
       01  PRINT-AREA2.
           05  FILLER                  PIC  X(132).

       SD  SORT-SCHDRPT.
       01  SORT-RECORD.
           05  SR-ST-MDY8              PIC  X(08).
           05  SR-ST-HMS8              PIC  X(08).
           05  SR-END-MDY8             PIC  X(08).
           05  SR-END-HMS8             PIC  X(08).
           05  SR-JOB                  PIC  X(08).
           05  SR-STREAM               PIC  X(08).
           05  SR-STATUS               PIC  X.
           05  SR-DURATION             PIC  X(05).
           05  FILLER REDEFINES SR-DURATION.
               10  SR-DUR-1            PIC  X.
               10  SR-DUR-2            PIC  X.
               10  SR-DUR-3X5          PIC  XXX.
           05  SR-DESC                 PIC  X(30).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING STORAGE JCI0057B START '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-HOLD-DAYS            PIC  XX       VALUE SPACE.
           05  WS-HOUR                 PIC  9(04)    VALUE ZERO.
           05  WS-MIN                  PIC  9(06)    VALUE ZERO.
           05  WS-REMAIN               PIC  999      VALUE ZERO.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-SORT-RCD-CNT         PIC S9(07)    VALUE ZERO.

       01  WS-PRINT-CONTROL            COMP.
           05  WS-LINE-SUM             PIC S99       VALUE +90.
           05  WS-LINE-MAX             PIC S99       VALUE +57.
           05  WS-PAGE-CNT             PIC S9(05)    VALUE ZERO.
           05  WS-SPACE-CONTROL        PIC S99       VALUE ZERO.

       01  WS-SUBSCRIPTS               COMP.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(05)    VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-SCHDRPT-SW       PIC  X        VALUE 'N'.
               88  END-OF-SCHDRPT                    VALUE 'Y'.
               88  MORE-SCHDRPT-RCDS                 VALUE 'N'.
           05  WS-END-SORT-SW          PIC  X        VALUE 'N'.
               88  END-OF-SORT                       VALUE 'Y'.
               88  MORE-SORT-RCDS                    VALUE 'N'.
           05  WS-FIRST-SW             PIC  X        VALUE 'Y'.
               88  FIRST-RECORD                      VALUE 'Y'.

       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-ERROR      PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-CYMD       PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

      ****************************************
      *    STANDARD MIS HEADINGS (8.5" X 14")
      ****************************************
       01  HEADING-LINE-1.
           05  FILLER                  PIC  X(08)    VALUE 'JCI0057B'.
           05  FILLER                  PIC  X(08)    VALUE '  DATE  '.
           05  H1-DATE                 PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X(29)    VALUE SPACE.
           05  FILLER                  PIC  X(26)    VALUE
               '  MERCY HEALTH PARTNERS   '.
           05  FILLER                  PIC  X(43)    VALUE SPACE.
           05  FILLER                  PIC  X(05)    VALUE 'PAGE '.
           05  H1-PAGE                 PIC  ZZZZ9    VALUE ZERO.

       01  APPEND-LINE-1.
           05  FILLER                  PIC  X        VALUE SPACE.
           05  FILLER                  PIC  X(08)    VALUE 'JCI0057B'.
           05  FILLER                  PIC  X(08)    VALUE '  DATE  '.
           05  A1-DATE                 PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X(28)    VALUE SPACE.
           05  FILLER                  PIC  X(26)    VALUE
               '     SCHDREPT SUMMARY     '.
           05  FILLER                  PIC  X(53)    VALUE SPACE.

       01  HEADING-LINE-2.
           05  H2-SECURITY-CODE        PIC  X(20)    VALUE SPACE.
           05  FILLER                  PIC  X(31)    VALUE SPACE.
           05  FILLER                  PIC  X(30)    VALUE
               '     INFORMATION SERVICES     '.
           05  FILLER                  PIC  X(51)     VALUE SPACE.

       01  HEADING-LINE-4.
           05  FILLER                  PIC  X(33)    VALUE
               '  START             END          '.
           05  FILLER                  PIC  X(33)    VALUE
               '      HH:MM     JOB       STREAM '.
           05  FILLER                  PIC  X(33)    VALUE
               '   STATUS  DESCRIPTION           '.
           05  FILLER                  PIC  X(33)    VALUE
               '                                 '.

       01  HEADING-LINE-5.
           05  FILLER                  PIC  X(33)    VALUE
               '  ----------------- -------------'.
           05  FILLER                  PIC  X(33)    VALUE
               '----  -----     --------  -------'.
           05  FILLER                  PIC  X(33)    VALUE
               '-  ------  ----------------------'.
           05  FILLER                  PIC  X(33)    VALUE
               '--------                         '.

       01  DETAIL-LINE-1.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-ST-MDY8              PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X        VALUE SPACE.
           05  D1-ST-HMS8              PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X        VALUE SPACE.
           05  D1-END-MDY8             PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X        VALUE SPACE.
           05  D1-END-HMS8             PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-DURATION             PIC  X(05)    VALUE SPACE.
           05  FILLER REDEFINES D1-DURATION.
               10  D1-DUR-1            PIC  X.
               10  D1-DUR-2            PIC  X.
               10  D1-DUR-3X5          PIC  XXX.
           05  FILLER                  PIC  X(05)    VALUE SPACE.
           05  D1-JOB                  PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-STREAM               PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-STATUS               PIC  X        VALUE SPACE.
           05  FILLER                  PIC  X(07)    VALUE SPACE.
           05  D1-DESC                 PIC  X(30)    VALUE SPACE.
           05  FILLER                  PIC  X(25)    VALUE SPACE.

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  JCI0057B END  '.

       LINKAGE SECTION.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0400-PROCESS-SCHDRPT.

           DISPLAY '**** RECORDS IN SCHDRPT  ' WS-READ-RCD-CNT.
           DISPLAY '**** RECORDS SORTED      ' WS-SORT-RCD-CNT.
           DISPLAY '**** JCI0057B EOJ ENCOUNTERED '.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** JCI0057B COND CODE ' WS-RETURN-CODE
                       ' SENT BY PROGRAM'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  SCHDRPT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** JCI0057B OPEN ERROR ON SCHDRPT'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT PRINT-SCHDRP2.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** JCI0057B OPEN ERROR ON SCHDRP2'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN OUTPUT PRINT-SCHDRPT.

       0300-CLOSE-FILES.
           CLOSE PRINT-SCHDRPT.
           CLOSE PRINT-SCHDRP2.
           CLOSE SCHDRPT.

       0400-PROCESS-SCHDRPT.
           PERFORM 0500-GET-PARM-CYMD.
           IF WS-RETURN-CODE = ZERO
               SORT SORT-SCHDRPT
                   ON ASCENDING KEY SR-ST-MDY8
                                    SR-ST-HMS8
                   INPUT PROCEDURE IS 0600-SORT-IN
                   OUTPUT PROCEDURE IS 1500-SORT-OUT.

       0500-GET-PARM-CYMD.
           MOVE SPACE                     TO WS-STV0400B-FIELDS.
           MOVE 'M/D/Y '                  TO STV0400B-OUT.
           CALL 'STV0400B' USING WS-STV0400B-FIELDS.
           MOVE STV0400B-DATA             TO H1-DATE A1-DATE.

       0600-SORT-IN.
           PERFORM 0700-READ-SCHDRPT THRU
                   0700-READ-SCHDRPT-EXIT
                   UNTIL END-OF-SCHDRPT.

       0700-READ-SCHDRPT.
           READ SCHDRPT.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
               PERFORM 0800-SELECT THRU
                       0800-SELECT-EXIT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-SCHDRPT-SW
           ELSE
               DISPLAY '**** JCI0057B READ ERROR ON SCHDRPT '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0700                     TO WS-RETURN-CODE.

       0700-READ-SCHDRPT-EXIT. EXIT.

       0800-SELECT.
           IF SCHDRPT-SLASH1 = '/'
               AND SCHDRPT-SLASH2 = '/'
               AND SCHDRPT-COLON1 = ':'
               AND SCHDRPT-COLON2 = ':'
               PERFORM 0900-DETERMINE-DURATION THRU
                       0900-DETERMINE-EXIT
               PERFORM 1100-SEND-TO-SORT.

       0800-SELECT-EXIT. EXIT.

       0900-DETERMINE-DURATION.
           MOVE SPACE                         TO SR-DURATION.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           IF SCHDRPT-ST-MDY8 = SCHDRPT-END-MDY8
               MOVE '00'                      TO WS-HOLD-DAYS
           ELSE
               MOVE SCHDRPT-ST-MDY8           TO STV0400B-DATA
               MOVE SCHDRPT-END-MDY8          TO STV0400B-DATA2
               MOVE 'M/D/Y '                  TO STV0400B-IN
               MOVE 'DAYS  '                  TO STV0400B-OUT
               CALL 'STV0400B' USING WS-STV0400B-FIELDS
               IF STV0400B-ERROR = 'ERROR'
                   DISPLAY '**** ERROR CONVERTING DATES '
                   WS-STV0400B-FIELDS
                   MOVE +1000                 TO WS-RETURN-CODE
                   GO TO 0900-DETERMINE-EXIT
               ELSE
                   MOVE STV0400B-DAYS(4: 2)   TO WS-HOLD-DAYS.
           MOVE SPACE                         TO WS-STV0400B-FIELDS.
           MOVE SCHDRPT-ST-HMS8(1: 2)         TO STV0400B-DATA(1: 2).
           MOVE SCHDRPT-ST-HMS8(4: 2)         TO STV0400B-DATA(3: 2).
           MOVE SCHDRPT-ST-HMS8(7: 2)         TO STV0400B-DATA(5: 2).
           MOVE WS-HOLD-DAYS                  TO STV0400B-DATA(7: 2).
           MOVE SCHDRPT-END-HMS8(1: 2)        TO STV0400B-DATA2(1: 2).
           MOVE SCHDRPT-END-HMS8(4: 2)        TO STV0400B-DATA2(3: 2).
           MOVE SCHDRPT-END-HMS8(7: 2)        TO STV0400B-DATA2(5: 2).
           MOVE 'HMSM  '                      TO STV0400B-IN.
           MOVE 'MIN   '                      TO STV0400B-OUT.
           CALL 'STV0400B' USING WS-STV0400B-FIELDS
           IF STV0400B-ERROR = 'ERROR'
               DISPLAY '**** ERROR CONVERTING TIMES '
               WS-STV0400B-FIELDS
               MOVE +0900                     TO WS-RETURN-CODE
               GO TO 0900-DETERMINE-EXIT
           ELSE
           IF STV0400B-DATA(1: 6) NUMERIC
               MOVE STV0400B-DATA(1: 6)       TO WS-MIN
               DIVIDE WS-MIN BY 60 GIVING WS-HOUR
                      REMAINDER WS-REMAIN
               MOVE WS-HOUR(3: 2)             TO SR-DURATION(1: 2)
               MOVE ':'                       TO SR-DURATION(3: 1)
               MOVE WS-REMAIN(2: 2)           TO SR-DURATION(4: 2).

       0900-DETERMINE-EXIT. EXIT.

       1100-SEND-TO-SORT.
           MOVE SCHDRPT-STREAM                TO SR-STREAM.
           MOVE SCHDRPT-JOB                   TO SR-JOB.
           MOVE SCHDRPT-STATUS                TO SR-STATUS.
           MOVE SCHDRPT-DESC                  TO SR-DESC.
           MOVE SCHDRPT-ST-MDY8               TO SR-ST-MDY8.
           MOVE SCHDRPT-ST-HMS8               TO SR-ST-HMS8.
           MOVE SCHDRPT-END-MDY8              TO SR-END-MDY8.
           MOVE SCHDRPT-END-HMS8              TO SR-END-HMS8.
           RELEASE SORT-RECORD.

       1500-SORT-OUT.
           PERFORM 1600-RETURN-RECORDS
               UNTIL END-OF-SORT.

           IF WS-RETURN-CODE = ZERO
               IF WS-SORT-RCD-CNT > ZERO
                   CONTINUE
               ELSE
                   PERFORM 1900-PRINT-HEADINGS
                   MOVE '     NO RECORDS MATCH SELECTION CRITERIA'
                                              TO PRINT-AREA
                   PERFORM 2000-WRITE-LINE.

       1600-RETURN-RECORDS.
           PERFORM 1700-RETURN-SORT.
           IF MORE-SORT-RCDS
               ADD 1                          TO WS-SORT-RCD-CNT
               PERFORM 1800-PRINT-DETAIL-LINE.

       1700-RETURN-SORT.
           RETURN SORT-SCHDRPT
               AT END
                   MOVE 'Y'                   TO WS-END-SORT-SW.

       1800-PRINT-DETAIL-LINE.
           IF WS-LINE-SUM > WS-LINE-MAX
               PERFORM 1900-PRINT-HEADINGS.
           MOVE SR-STREAM                     TO D1-STREAM.
           MOVE SR-JOB                        TO D1-JOB.
           MOVE SR-STATUS                     TO D1-STATUS.
           IF SR-DURATION = '00:00' OR '00:01'
               MOVE SPACE                     TO D1-DURATION
           ELSE
           IF SR-DUR-1 = '0'
               MOVE SR-DURATION               TO D1-DURATION
               IF SR-DUR-2 = '0'
                   MOVE SPACE                 TO D1-DUR-1 D1-DUR-2
               ELSE
                   MOVE SPACE                 TO D1-DUR-1
           ELSE
               MOVE SR-DURATION               TO D1-DURATION.
           MOVE SR-DESC                       TO D1-DESC.
           MOVE SR-ST-MDY8                    TO D1-ST-MDY8.
           MOVE SR-ST-HMS8                    TO D1-ST-HMS8.
           MOVE SR-END-MDY8                   TO D1-END-MDY8.
           MOVE SR-END-HMS8                   TO D1-END-HMS8.
           MOVE DETAIL-LINE-1                 TO PRINT-AREA.
           PERFORM 2000-WRITE-LINE.
           MOVE DETAIL-LINE-1                 TO PRINT-AREA2.
           PERFORM 2000-APPEND-LINE.

       1900-PRINT-HEADINGS.
           ADD 1                              TO WS-PAGE-CNT.
           MOVE 1                             TO WS-SPACE-CONTROL.
           MOVE WS-PAGE-CNT                   TO H1-PAGE.
           MOVE HEADING-LINE-1                TO PRINT-AREA.
           PERFORM 2100-WRITE-PAGE-TOP.
           MOVE HEADING-LINE-2                TO PRINT-AREA.
           PERFORM 2000-WRITE-LINE.
           MOVE 2                             TO WS-SPACE-CONTROL.
           MOVE HEADING-LINE-4                TO PRINT-AREA.
           PERFORM 2000-WRITE-LINE.
           MOVE 1                             TO WS-SPACE-CONTROL.
           MOVE HEADING-LINE-5                TO PRINT-AREA.
           PERFORM 2000-WRITE-LINE.
           IF FIRST-RECORD
               MOVE 'N'                       TO WS-FIRST-SW
               MOVE APPEND-LINE-1             TO PRINT-AREA2
               PERFORM 2000-APPEND-LINE
               MOVE SPACE                     TO PRINT-AREA2
               PERFORM 2000-APPEND-LINE
               MOVE HEADING-LINE-4            TO PRINT-AREA2
               PERFORM 2000-APPEND-LINE
               MOVE HEADING-LINE-5            TO PRINT-AREA2
               PERFORM 2000-APPEND-LINE.

       2000-WRITE-LINE.
           WRITE PRINT-AREA
               AFTER ADVANCING WS-SPACE-CONTROL LINES.
           ADD WS-SPACE-CONTROL               TO WS-LINE-SUM.

       2000-APPEND-LINE.
           WRITE PRINT-AREA2.

       2100-WRITE-PAGE-TOP.
           WRITE PRINT-AREA
               AFTER ADVANCING PAGE-TOP.
           MOVE 1                             TO WS-LINE-SUM.

      ************************************ END JCI0057B
