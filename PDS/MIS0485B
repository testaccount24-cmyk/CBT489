
       TITLE '  MIS0485B: COMPARE SUT CHANGE LIST'.
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    MIS0485B.
      ******************************************************************
      *@@@ II COMPARE SUT CHANGE LIST
      ******************************************************************
      *
      *  I AM INTERESTED IN KNOWING IF/WHERE THESE PROGRAMS
      *  ARE IN USE.     EMAIL:  jiannon@me.com
      *
      ******************************************************************
      * DATE WRITTEN: 08/19/96
      * AUTHOR      : JIM IANNONE
      *               LOOP THROUGH A SUT DELIVERY LIST AND COMPARE
      *               TO AN APPLICATION MAINTAINED LIST OF ITEMS
      *               FOR CHANGE NOTIFICATION PURPOSES.
      ******************************************************************
      *               10/08/1999
      *               LOOP THROUGH AND GATHER GENERIC ELEMENT ENTRIES
      *               FIRST.  THEN COMPARE THE GENERIC ELEMENTS TO
      *               EACH DELIVERED ELEMENT.
      ******************************************************************

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS PAGE-TOP.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT DELIVER              ASSIGN TO DELIVER
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SMSSUT               ASSIGN TO SMSSUT
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT SMSSUT2              ASSIGN TO SMSSUT2
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT CPY2PRD              ASSIGN TO CPY2PRD
                                       ORGANIZATION IS SEQUENTIAL
                                       ACCESS MODE IS SEQUENTIAL
                                       FILE STATUS IS WS-STAT.

           SELECT PRINT-MATCH          ASSIGN TO PRINTER.

       DATA DIVISION.
       FILE SECTION.

       FD  DELIVER
           RECORDING MODE F.
       01  DELIVER-RECORD.
           05  FILLER              PIC  X.
           05  DELIVER-SUT-NAME    PIC  X(20).
           05  FILLER              PIC  X.
           05  DELIVER-LIB         PIC  X(08).
           05  FILLER              PIC  X.
           05  DELIVER-MARK1       PIC  X.
           05  FILLER              PIC  X.
           05  DELIVER-ELEMENT     PIC  X(08).
           05  FILLER              PIC  X.
           05  DELIVER-MARK2       PIC  X.
           05  FILLER              PIC  X.
           05  DELIVER-LIB-NAME    PIC  X(44).
           05  DELIVER-TRAILER.
               10  FILLER              PIC  X.
               10  DELIVER-MARK3       PIC  X.
               10  FILLER              PIC  X.
               10  DELIVER-TARGET      PIC  X(05).
               10  FILLER              PIC  X.
               10  DELIVER-MARK4       PIC  X.
               10  FILLER              PIC  X.
               10  DELIVER-AREA1       PIC  XXX.
               10  FILLER              PIC  X.
               10  DELIVER-MOVE-REQ1   PIC  X(05).
               10  FILLER              PIC  X.
               10  DELIVER-AREA2       PIC  XXX.
               10  FILLER              PIC  X.
               10  DELIVER-MOVE-REQ2   PIC  X(05).
               10  FILLER              PIC  X.
               10  DELIVER-AREA3       PIC  XXX.
               10  FILLER              PIC  X.
               10  DELIVER-MOVE-REQ3   PIC  X(05).
               10  FILLER              PIC  X(04).
           05  DELIVER-TRAILER2 REDEFINES DELIVER-TRAILER.
               10  DELIVER-DESCRIPTION PIC  X(40).
               10  FILLER              PIC  X(04).

       FD  SMSSUT
           RECORDING MODE F.
       01  SMSSUT-RECORD.
           05  SMSSUT-DOC              PIC  X.
           05  FILLER                  PIC  X.
           05  SMSSUT-RESP-PARTY       PIC  X(10).
           05  FILLER                  PIC  X.
           05  SMSSUT-LIB              PIC  X(08).
           05  FILLER                  PIC  X.
           05  SMSSUT-ELEMENT          PIC  X(08).
           05  FILLER                  PIC  X.
           05  SMSSUT-TARGET           PIC  X(05).
           05  FILLER                  PIC  XX.
           05  SMSSUT-COMMENT          PIC  X(32).
           05  FILLER                  PIC  X(10).

       FD  SMSSUT2
           RECORDING MODE F.
       01  SMSSUT2-RECORD.
           05  SMSSUT2-DOC             PIC  X.
           05  FILLER                  PIC  X.
           05  SMSSUT2-RESP-PARTY      PIC  X(10).
           05  FILLER                  PIC  X.
           05  SMSSUT2-LIB             PIC  X(08).
           05  FILLER                  PIC  X.
           05  SMSSUT2-ELEMENT         PIC  X(08).
           05  FILLER                  PIC  X.
           05  SMSSUT2-TARGET          PIC  X(05).
           05  FILLER                  PIC  XX.
           05  SMSSUT2-COMMENT         PIC  X(32).
           05  FILLER                  PIC  X(10).

       FD  CPY2PRD
           RECORDING MODE F.
       01  CPY2PRD-RECORD.
           05  CPY2PRD-RCD             PIC  X(80).

       FD  PRINT-MATCH
           RECORDING MODE F.
       01  PRINT-AREA.
           05  FILLER                  PIC  X(80).

       WORKING-STORAGE SECTION.

       01  WS-START.
           05  FILLER                  PIC  X(32)    VALUE
               ' WORKING STORAGE MIS0485B START '.

       01  WS-WORK-FIELDS.
           05  WS-STAT                 PIC  99       VALUE ZERO.
           05  WS-RETURN-CODE          PIC S9(04)    VALUE ZERO COMP.
           05  WS-TALLY1               PIC S9(04)    VALUE ZERO COMP.
           05  WS-TALLY2               PIC S9(04)    VALUE ZERO COMP.
           05  WS-HOLD-LIB             PIC  X(09)    VALUE SPACE.
           05  WS-HOLD-ELEMENT         PIC  X(09)    VALUE SPACE.
           05  FILLER REDEFINES WS-HOLD-ELEMENT.
               10  WS-HOLD-ELEM-1X2    PIC  XX.
               10  WS-HOLD-ELEM-3      PIC  X.
               10  WS-HOLD-ELEM-4X9    PIC  X(06).
           05  FILLER REDEFINES WS-HOLD-ELEMENT.
               10  WS-HOLD-ELEM-1      PIC  X.
               10  WS-HOLD-ELEM-2X9    PIC  X(08).
           05  WS-HOLD-ORIG-ELEM       PIC  X(09)    VALUE SPACE.
           05  WS-HOLD-RESP            PIC  X(10)    VALUE SPACE.
           05  WS-HOLD-TGT             PIC  X(05)    VALUE SPACE.
           05  WS-HOLD-CMNT            PIC  X(32)    VALUE SPACE.

       01  WS-COUNT-FIELDS             COMP-3.
           05  WS-READ-RCD-CNT         PIC S9(07)    VALUE ZERO.
           05  WS-READ-RCD-CNT2        PIC S9(07)    VALUE ZERO.
           05  WS-WRITE-RCD-CNT        PIC S9(07)    VALUE ZERO.
           05  WS-SUB1                 PIC S9(05)    VALUE ZERO.
           05  WS-SUB2                 PIC S9(05)    VALUE ZERO.
           05  WS-GEN-MAX              PIC S9(05)    VALUE ZERO.
           05  WS-SKIP-CNT             PIC  9(05)    VALUE ZERO.
           05  WS-GEN-MATCH-CNT        PIC  9(05)    VALUE ZERO.

       01  WS-PRINT-CONTROL            COMP.
           05  WS-LINE-SUM             PIC S99       VALUE +90.
           05  WS-LINE-MAX             PIC S99       VALUE +57.
           05  WS-PAGE-CNT             PIC S9(05)    VALUE ZERO.
           05  WS-SPACE-CONTROL        PIC S99       VALUE ZERO.

       01  WS-SWITCHES.
           05  WS-END-DELIVER-SW       PIC  X        VALUE 'N'.
               88  END-OF-DELIVER                    VALUE 'Y'.
               88  MORE-DELIVER-RCDS                 VALUE 'N'.
           05  WS-END-CPY2PRD-SW       PIC  X        VALUE 'N'.
               88  END-OF-CPY2PRD                    VALUE 'Y'.
               88  MORE-CPY2PRD-RCDS                 VALUE 'N'.
           05  WS-READ-DELIVER-SW      PIC  X        VALUE 'Y'.
           05  WS-READ-SMSSUT-SW       PIC  X        VALUE 'Y'.
           05  WS-FIRST-RCD-SW         PIC  X        VALUE 'Y'.
               88  FIRST-RECORD                      VALUE 'Y'.
           05  WS-DIFF-SW              PIC  X        VALUE SPACE.
           05  WS-MATCH-SW             PIC  X        VALUE SPACE.
           05  WS-LOAD-SW              PIC  X        VALUE SPACE.

      ****************************************
      *    STANDARD MIS HEADINGS (8.5" X 11")
      ****************************************
       01  HEADING-LINE-1.
           05  FILLER                  PIC  X(08)    VALUE 'MIS0485B'.
           05  FILLER                  PIC  X(08)    VALUE '  DATE  '.
           05  H1-DATE                 PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  X(03)    VALUE SPACE.
           05  FILLER                  PIC  X(26)    VALUE
               'ST. VINCENT MEDICAL CENTER'.
           05  FILLER                  PIC  X(17)    VALUE SPACE.
           05  FILLER                  PIC  X(05)    VALUE 'PAGE '.
           05  H1-PAGE                 PIC  ZZZZ9    VALUE ZERO.

       01  HEADING-LINE-2.
           05  FILLER                  PIC  X(25)    VALUE SPACE.
           05  FILLER                  PIC  X(30)    VALUE
               'MANAGEMENT INFORMATION SYSTEMS'.
           05  FILLER                  PIC  X(25)     VALUE SPACE.

       01  HEADING-LINE-3.
           05  FILLER                  PIC  X        VALUE SPACE.
           05  H3-TITLE                PIC  X(79)    VALUE SPACE.

       01  HEADING-LINE-4.
           05  FILLER                  PIC  X(40)    VALUE
               '  RESP PARTY  LIBRARY   ELEMENT   TARGET'.
           05  FILLER                  PIC  X(40)    VALUE
               '  COMMENT                               '.

       01  HEADING-LINE-5.
           05  FILLER                  PIC  X(40)    VALUE
               '  ----------  --------  --------  ------'.
           05  FILLER                  PIC  X(40)    VALUE
               '  --------------------------------      '.

       01  WS-LOAD-MSG1.
           05  FILLER                  PIC  X(40)    VALUE
               '  Warning!!  This SUT deliveres modules '.
           05  FILLER                  PIC  X(40)    VALUE
               'to P0000.LOAD libraries.                '.

       01  WS-LOAD-MSG2.
           05  FILLER                  PIC  X(40)    VALUE
               '             Move E0000.LOAD members to '.
           05  FILLER                  PIC  X(40)    VALUE
               'P0000 libraries before installing SUT.  '.

       01  DETAIL-LINE-1.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-RESP-PARTY           PIC  X(10)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-LIB                  PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-ELEMENT              PIC  X(08)    VALUE SPACE.
           05  FILLER                  PIC  XX       VALUE SPACE.
           05  D1-TARGET               PIC  X(05)    VALUE SPACE.
           05  FILLER                  PIC  XXX      VALUE SPACE.
           05  D1-COMMENT              PIC  X(32)    VALUE SPACE.
           05  FILLER                  PIC  X(06)    VALUE SPACE.

      *    YEAR 2000 COMPATIBLE DATE ROUTINE
       01  WS-STV0400B-FIELDS.
           05  STV0400B-DATA           PIC  X(10)   VALUE SPACE.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-ERROR      PIC  X(06).
               10  STV0400B-NBR        PIC  X(04).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-CYMD       PIC  X(08).
               10  FILLER              PIC  XX.
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAYS       PIC  X(05).
               10  FILLER              PIC  X(05).
           05  FILLER REDEFINES STV0400B-DATA.
               10  STV0400B-DAY        PIC  X(09).
               10  STV0400B-DAY-NBR    PIC  X.
           05  STV0400B-DATA2          PIC  X(10)   VALUE SPACE.
           05  STV0400B-IN             PIC  X(06)   VALUE SPACE.
           05  STV0400B-OUT            PIC  X(06)   VALUE SPACE.

      *    DATASET MASK COMPARISON
       01  WS-MIS0540B-FIELDS.
           05  MIS0540B-MASK           PIC  X(44).
           05  MIS0540B-DSN            PIC  X(44).
           05  MIS0540B-DISPLAY        PIC  X.
           05  MIS0540B-EDIT-NAMES     PIC  X.
           05  MIS0540B-MATCH          PIC  XXX.

       01  WS-TABLES.
           05  WS-GENERIC-TABLE        OCCURS 40 TIMES.
               10  WS-GEN-RESP         PIC  X(10).
               10  WS-GEN-LIB          PIC  X(08).
               10  WS-GEN-ELEM         PIC  X(08).
               10  WS-GEN-TGT          PIC  X(05).
               10  WS-GEN-CMNT         PIC  X(32).

       01  WS-END.
           05  FILLER                  PIC  X(16)    VALUE
               '  MIS0485B END  '.

      /
       PROCEDURE DIVISION.

       0100-PROGRAM-ENTRY.
           PERFORM 0200-OPEN-FILES.

           IF WS-RETURN-CODE = ZERO
               MOVE SPACE                     TO WS-STV0400B-FIELDS
               MOVE 'M/D/Y '                  TO STV0400B-OUT
               CALL 'STV0400B' USING WS-STV0400B-FIELDS
               MOVE STV0400B-DATA             TO H1-DATE
               PERFORM 0400-PROCESS-DELIVER.

           DISPLAY '**** ELEMENT RECORDS     ' WS-READ-RCD-CNT.
           DISPLAY '**** SMSSUT ENTRIES      ' WS-READ-RCD-CNT2.
           DISPLAY '**** GENERIC SKIPS       ' WS-SKIP-CNT.
           DISPLAY '**** GENERIC ENTRIES     ' WS-GEN-MAX.
           DISPLAY '**** GENERIC MATCHES     ' WS-GEN-MATCH-CNT.
           DISPLAY '**** MATCHES             ' WS-WRITE-RCD-CNT.
           DISPLAY '**** MIS0485B EOJ ENCOUNTERED '.

           IF WS-RETURN-CODE = ZERO
               PERFORM 0300-CLOSE-FILES
           ELSE
               DISPLAY '**** MIS0485B COND CODE ' WS-RETURN-CODE
                       ' SENT BY PROGRAM'
               MOVE WS-RETURN-CODE            TO RETURN-CODE.

           STOP RUN.

       0200-OPEN-FILES.
           OPEN INPUT  DELIVER.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0485B OPEN ERROR ON DELIVER'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0200                     TO WS-RETURN-CODE.
           OPEN INPUT SMSSUT.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0485B OPEN ERROR ON SMSSUT'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0201                     TO WS-RETURN-CODE.
           OPEN INPUT SMSSUT2.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0485B OPEN ERROR ON SMSSUT2'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0202                     TO WS-RETURN-CODE.
           OPEN INPUT CPY2PRD.
           IF WS-STAT NOT = ZERO
               DISPLAY '**** MIS0485B OPEN ERROR ON CPY2PRD'
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0203                     TO WS-RETURN-CODE.
           OPEN OUTPUT PRINT-MATCH.

       0300-CLOSE-FILES.
           CLOSE PRINT-MATCH.
           CLOSE CPY2PRD.
           CLOSE SMSSUT2.
           CLOSE SMSSUT.
           CLOSE DELIVER.

       0400-PROCESS-DELIVER.
           IF WS-RETURN-CODE = ZERO
               PERFORM 0600-SELECT THRU
                       0600-SELECT-EXIT
                       UNTIL END-OF-DELIVER
               IF WS-WRITE-RCD-CNT = ZERO
                   PERFORM 1400-PRINT-HEADINGS
                   MOVE '  NO SUT ELEMENTS MATCH THE SMSSUT LIST'
                                              TO PRINT-AREA
                   MOVE 2                     TO WS-SPACE-CONTROL
                   PERFORM 1500-WRITE-LINE.
           IF WS-RETURN-CODE = ZERO
               MOVE SPACE                     TO WS-LOAD-SW
               PERFORM 1200-READ-CPY2PRD THRU
                       1200-READ-CPY2PRD-EXIT
                       UNTIL END-OF-CPY2PRD
               IF WS-LOAD-SW = 'Y'
                   MOVE SPACE                 TO PRINT-AREA
                   PERFORM 1500-WRITE-LINE
                   MOVE WS-LOAD-MSG1          TO PRINT-AREA
                   PERFORM 1500-WRITE-LINE
                   MOVE WS-LOAD-MSG2          TO PRINT-AREA
                   PERFORM 1500-WRITE-LINE.


       0600-SELECT.
           IF WS-READ-DELIVER-SW = 'Y'
               PERFORM 1000-READ-DELIVER.
           IF WS-READ-SMSSUT-SW = 'Y'
               PERFORM 1000-READ-SMSSUT.

           IF FIRST-RECORD
               STRING 'SMS SUT DELIVERY COMPARISON: ' DELIMITED BY SIZE
                   DELIVER-SUT-NAME                   DELIMITED BY SIZE
                   INTO H3-TITLE
               CALL 'STV0270B' USING H3-TITLE
               IF DELIVER-SUT-NAME = 'DELIVERY REPORT'
                   MOVE 'Y'                       TO WS-DIFF-SW.
           IF FIRST-RECORD
               MOVE 'N'                       TO WS-FIRST-RCD-SW
               MOVE ZERO                      TO WS-SUB1
               PERFORM 0800-GET-GENERICS THRU
                       0800-GET-GENERICS-EXIT.

           IF SMSSUT-DOC = '*'
               OR SMSSUT-LIB = SPACE
               MOVE 'N'                       TO WS-READ-DELIVER-SW
               MOVE 'Y'                       TO WS-READ-SMSSUT-SW
               GO TO 0600-SELECT-EXIT.

      *---------------------------------------
      *    COMPARE ALL DELIVERED ELEMENTS WITH THE GENERICS
      *    TABLE PREVIOUSLY LOADED
      *+--------------------------------------
           MOVE 'N'                           TO WS-MATCH-SW.
           IF WS-READ-DELIVER-SW = 'Y'
               PERFORM 0900-TEST-GENERIC THRU
                       0900-TEST-GENERIC-EXIT
                       VARYING WS-SUB1 FROM 1 BY 1
                       UNTIL WS-SUB1 > WS-GEN-MAX.

           MOVE 'N'                           TO WS-MATCH-SW.
           IF WS-READ-SMSSUT-SW = 'Y'
               MOVE SMSSUT-LIB                TO WS-HOLD-LIB
               MOVE SMSSUT-ELEMENT            TO WS-HOLD-ELEMENT
               MOVE SMSSUT-ELEMENT            TO WS-HOLD-ORIG-ELEM
               PERFORM 0700-CHANGE-ELEMENT THRU
                       0700-CHANGE-EXIT.

           MOVE 'N'                           TO WS-READ-DELIVER-SW
                                                 WS-READ-SMSSUT-SW.
           IF END-OF-DELIVER
               GO TO 0600-SELECT-EXIT.

           IF DELIVER-LIB > SMSSUT-LIB
               MOVE 'Y'                       TO WS-READ-SMSSUT-SW
               GO TO 0600-SELECT-EXIT.
           IF DELIVER-LIB < SMSSUT-LIB
               MOVE 'Y'                       TO WS-READ-DELIVER-SW
               GO TO 0600-SELECT-EXIT.

      *---------------------------------------
      *    SKIP GENERICS THAT WERE PREVIOUSLY LOADED BY
      *    LOOPING THROUGH THE SMSSUT2 FILE
      *+--------------------------------------
           IF WS-HOLD-ELEM-1 NOT = '*'
               MOVE ZERO                      TO WS-TALLY1 WS-TALLY2
               INSPECT WS-HOLD-ELEM-2X9 TALLYING WS-TALLY1 FOR ALL '*'
               INSPECT WS-HOLD-ELEM-2X9 TALLYING WS-TALLY2 FOR ALL '%'
               ADD WS-TALLY1                  TO WS-TALLY2
               IF WS-TALLY2 > ZERO
                   MOVE 'Y'                   TO WS-READ-SMSSUT-SW
                   ADD 1                      TO WS-SKIP-CNT
                   GO TO 0600-SELECT-EXIT.

           IF DELIVER-ELEMENT > WS-HOLD-ELEMENT
               MOVE 'Y'                       TO WS-READ-SMSSUT-SW
           ELSE
           IF DELIVER-ELEMENT < WS-HOLD-ELEMENT
               MOVE 'Y'                       TO WS-READ-DELIVER-SW
           ELSE
               MOVE SMSSUT-RESP-PARTY         TO WS-HOLD-RESP
               MOVE SMSSUT-TARGET             TO WS-HOLD-TGT
               MOVE SMSSUT-COMMENT            TO WS-HOLD-CMNT
               MOVE 'Y'                       TO WS-READ-SMSSUT-SW
               PERFORM 1100-CHECK-TARGET THRU
                       1100-CHECK-TARGET-EXIT.

       0600-SELECT-EXIT. EXIT.

       0700-CHANGE-ELEMENT.
           IF WS-DIFF-SW = 'Y'
               GO TO 0700-CHANGE-EXIT.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A6$ ' BY 'DG$ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A7$ ' BY 'DG$ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A6E ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A7E ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A6T ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A7D ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A7S ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL 'A7W ' BY 'DG@ '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#01 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#02 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#03 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#04 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#05 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '#06 ' BY '#CC '.
           INSPECT WS-HOLD-ELEMENT REPLACING ALL '$01 ' BY '$CP '.
           IF WS-HOLD-LIB = 'SYSJOBS '
               IF WS-HOLD-ELEM-3 = 'T'
                   MOVE 'P'                   TO WS-HOLD-ELEM-3
               ELSE
               IF WS-HOLD-ELEM-3 = 'E'
                   MOVE 'D'                   TO WS-HOLD-ELEM-3.

       0700-CHANGE-EXIT. EXIT.

       0800-GET-GENERICS.
           READ SMSSUT2.
           IF WS-STAT = ZERO
               CONTINUE
           ELSE
           IF WS-STAT = 10
               MOVE WS-SUB1                   TO WS-GEN-MAX
               GO TO 0800-GET-GENERICS-EXIT
           ELSE
               DISPLAY '**** MIS0485B READ ERROR ON SMSSUT2 '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE +0500                     TO WS-RETURN-CODE
               GO TO 0800-GET-GENERICS-EXIT.

           IF SMSSUT2-DOC = '*'
               OR SMSSUT2-LIB = SPACE
               GO TO 0800-GET-GENERICS.

           MOVE SMSSUT2-ELEMENT               TO WS-HOLD-ELEMENT.

           IF WS-HOLD-ELEM-1 NOT = '*'
               MOVE ZERO                      TO WS-TALLY1 WS-TALLY2
               INSPECT WS-HOLD-ELEM-2X9 TALLYING WS-TALLY1 FOR ALL '*'
               INSPECT WS-HOLD-ELEM-2X9 TALLYING WS-TALLY2 FOR ALL '%'
               ADD WS-TALLY1                  TO WS-TALLY2
               IF WS-TALLY2 > ZERO
                 IF WS-SUB1 < 40
                     ADD 1                    TO WS-SUB1
                     MOVE SMSSUT2-LIB         TO WS-HOLD-LIB
                     PERFORM 0700-CHANGE-ELEMENT THRU
                             0700-CHANGE-EXIT
                     MOVE WS-HOLD-ELEMENT     TO WS-GEN-ELEM(WS-SUB1)
                     MOVE SMSSUT2-RESP-PARTY  TO WS-GEN-RESP(WS-SUB1)
                     MOVE SMSSUT2-LIB         TO WS-GEN-LIB(WS-SUB1)
                     MOVE SMSSUT2-TARGET      TO WS-GEN-TGT(WS-SUB1)
                     MOVE SMSSUT2-COMMENT     TO WS-GEN-CMNT(WS-SUB1)
                 ELSE
                     DISPLAY '**** MIS0485B > 40 GENERIC ENTRIES '
                     MOVE +0800               TO WS-RETURN-CODE.

           GO TO 0800-GET-GENERICS.

       0800-GET-GENERICS-EXIT. EXIT.

       0900-TEST-GENERIC.
           IF DELIVER-LIB NOT = WS-GEN-LIB(WS-SUB1)
               GO TO 0900-TEST-GENERIC-EXIT.
           MOVE WS-GEN-ELEM(WS-SUB1)          TO MIS0540B-MASK.
           MOVE DELIVER-ELEMENT               TO MIS0540B-DSN
                                                 WS-HOLD-ORIG-ELEM.
           MOVE SPACE                         TO MIS0540B-DISPLAY
                                                 MIS0540B-MATCH
                                                 MIS0540B-EDIT-NAMES.
           CALL 'MIS0540B' USING MIS0540B-MASK MIS0540B-DSN
                           MIS0540B-DISPLAY MIS0540B-EDIT-NAMES
                           MIS0540B-MATCH.
           IF MIS0540B-MATCH NUMERIC
               DISPLAY '**** MIS0545B CALL ERROR TO MIS0540B'
               DISPLAY '**** CODE = ' MIS0540B-MATCH
               MOVE +0600                     TO WS-RETURN-CODE
           ELSE
           IF MIS0540B-MATCH = 'YES'
               MOVE WS-GEN-RESP(WS-SUB1)      TO WS-HOLD-RESP
               MOVE WS-GEN-TGT(WS-SUB1)       TO WS-HOLD-TGT
               MOVE WS-GEN-LIB(WS-SUB1)       TO WS-HOLD-LIB
               MOVE WS-GEN-CMNT(WS-SUB1)      TO WS-HOLD-CMNT
               MOVE 'Y'                       TO WS-MATCH-SW
               PERFORM 1100-CHECK-TARGET THRU
                       1100-CHECK-TARGET-EXIT.

       0900-TEST-GENERIC-EXIT. EXIT.

       1000-READ-DELIVER.
           READ DELIVER.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-DELIVER-SW
           ELSE
               DISPLAY '**** MIS0485B READ ERROR ON DELIVER '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE 'Y'                       TO WS-END-DELIVER-SW
               MOVE +1000                     TO WS-RETURN-CODE.

       1000-READ-SMSSUT.
           READ SMSSUT.
           IF WS-STAT = ZERO
               ADD 1                          TO WS-READ-RCD-CNT2
           ELSE
           IF WS-STAT = 10
               MOVE HIGH-VALUE                TO SMSSUT-RECORD
           ELSE
               DISPLAY '**** MIS0485B READ ERROR ON SMSSUT '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE HIGH-VALUE                TO SMSSUT-LIB
               MOVE +1000                     TO WS-RETURN-CODE.

       1100-CHECK-TARGET.
           IF WS-DIFF-SW = 'Y'
               PERFORM 1300-PRINT-DETAIL-LINE
               GO TO 1100-CHECK-TARGET-EXIT.
           IF WS-HOLD-TGT = SPACE OR 'BOTH '
               PERFORM 1300-PRINT-DETAIL-LINE.
           IF WS-HOLD-TGT = 'PROD '
               IF DELIVER-TARGET = 'PROD ' OR ' BOTH' OR SPACE
                   PERFORM 1300-PRINT-DETAIL-LINE.
           IF WS-HOLD-TGT = 'TEST '
               IF DELIVER-TARGET = 'TEST ' OR ' BOTH' OR SPACE
                   PERFORM 1300-PRINT-DETAIL-LINE.

       1100-CHECK-TARGET-EXIT. EXIT.

       1200-READ-CPY2PRD.
           READ CPY2PRD.
           IF WS-STAT = ZERO
               PERFORM 1200-CHECK-FOR-PLIBS
           ELSE
           IF WS-STAT = 10
               MOVE 'Y'                       TO WS-END-CPY2PRD-SW
           ELSE
               DISPLAY '**** MIS0485B READ ERROR ON CPY2PRD '
               DISPLAY '**** FILE STAT = ' WS-STAT
               MOVE 'Y'                       TO WS-END-CPY2PRD-SW
               MOVE +1200                     TO WS-RETURN-CODE.

       1200-READ-CPY2PRD-EXIT. EXIT.

      *---------------------------------------
      *    DETERMINE IF ANY LOAD MODULES ARE BEING DELIVERED
      *    DIRECTLY TO P0000 LIBRARIES RATHER THAN ENHANCEMENT
      *    E0000 LIBRARIES.  IF SO, IT IS IMPORTANT TO MOVE
      *    E0000 MODULES TO THEIR CORRESPONDING P0000 LIBRARIES
      *    BEFORE THE SUT APPLY, OR SOFTWARE MAY BE INADVERTANTLY
      *    BACKLEVELED SINCE E0000 LIBRARIES OVERRIDE P0000
      *    LIBRARIES IN CICS.
      *---------------------------------------
       1200-CHECK-FOR-PLIBS.
           IF CPY2PRD-RCD(1: 9) = '//SYSUT2 '
               PERFORM VARYING WS-SUB2 FROM 15 BY 1
                   UNTIL WS-SUB2 > 68
                   IF CPY2PRD-RCD(WS-SUB2: 10) = 'P0000.LOAD'
                       MOVE 'Y'               TO WS-LOAD-SW
                       MOVE 70                TO WS-SUB2
                   END-IF
               END-PERFORM
           END-IF.

       1300-PRINT-DETAIL-LINE.
           IF WS-LINE-SUM > WS-LINE-MAX
               PERFORM 1400-PRINT-HEADINGS.
           MOVE WS-HOLD-LIB                   TO D1-LIB.
           MOVE WS-HOLD-RESP                  TO D1-RESP-PARTY.
           MOVE WS-HOLD-ORIG-ELEM             TO D1-ELEMENT.
           MOVE WS-HOLD-CMNT                  TO D1-COMMENT.
           MOVE WS-HOLD-TGT                   TO D1-TARGET.
           MOVE DETAIL-LINE-1                 TO PRINT-AREA.
           ADD 1                              TO WS-WRITE-RCD-CNT.
           IF WS-MATCH-SW = 'Y'
               ADD 1                          TO WS-GEN-MATCH-CNT.
           PERFORM 1500-WRITE-LINE.

       1400-PRINT-HEADINGS.
           ADD 1                              TO WS-PAGE-CNT.
           MOVE 1                             TO WS-SPACE-CONTROL.
           MOVE WS-PAGE-CNT                   TO H1-PAGE.
           MOVE HEADING-LINE-1                TO PRINT-AREA.
           PERFORM 1600-WRITE-PAGE-TOP.
           MOVE HEADING-LINE-2                TO PRINT-AREA.
           PERFORM 1500-WRITE-LINE.
           MOVE 2                             TO WS-SPACE-CONTROL.
           MOVE HEADING-LINE-3                TO PRINT-AREA.
           PERFORM 1500-WRITE-LINE.
           MOVE HEADING-LINE-4                TO PRINT-AREA.
           PERFORM 1500-WRITE-LINE.
           MOVE 1                             TO WS-SPACE-CONTROL.
           MOVE HEADING-LINE-5                TO PRINT-AREA.
           PERFORM 1500-WRITE-LINE.

       1500-WRITE-LINE.
           WRITE PRINT-AREA
               AFTER ADVANCING WS-SPACE-CONTROL LINES.
           ADD WS-SPACE-CONTROL               TO WS-LINE-SUM.

       1600-WRITE-PAGE-TOP.
           WRITE PRINT-AREA
               AFTER ADVANCING PAGE-TOP.
           MOVE 1                             TO WS-LINE-SUM.

      ************************************ END MIS0485B
